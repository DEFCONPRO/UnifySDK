//Sensor multilevel CC
def zwSENSOR_MULTILEVEL_VERSION           0x3101
def zwSENSOR_MULTILEVEL_SENSOR_TYPE       0x3102
def zwSENSOR_MULTILEVEL_SUPPORTED_SCALES  0x3103
def zwSENSOR_MULTILEVEL_SCALE             0x3104
def zwSENSOR_MULTILEVEL_SENSOR_VALUE      0x3105
def zwMULTILEVEL_SUPPORTED_SENSOR_TYPES   0x3106

//Thermostat setpoint CC
def zwTHERMOSTAT_SETPOINT_VERSION           0x4301
def zwTHERMOSTAT_SUPPORTED_SETPOINT_TYPES   0x4302
def zwTHERMOSTAT_SETPOINT_TYPE              0x4303
def zwTHERMOSTAT_SETPOINT_VALUE             0x4304
def zwTHERMOSTAT_SETPOINT_VALUE_SCALE       0x4305
def zwTHERMOSTAT_SETPOINT_MIN_VALUE         0x4306
def zwTHERMOSTAT_SETPOINT_MIN_VALUE_SCALE   0x4307
def zwTHERMOSTAT_SETPOINT_MAX_VALUE         0x4308
def zwTHERMOSTAT_SETPOINT_MAX_VALUE_SCALE   0x4309

def zwTHERMOSTAT_MODE_VERSION     0x4001
def zwTHERMOSTAT_MODE             0x4002
def zwTHERMOSTAT_SUPPORTED_MODES  0x4003

// Thermostat Cluster
def zbTHERMOSTAT_CLUSTER_LOCAL_TEMPERATURE                      0x02010000
def zbTHERMOSTAT_CLUSTER_OCCUPIED_COOLING_SETPOINT              0x02010011
def zbTHERMOSTAT_CLUSTER_OCCUPIED_HEATING_SETPOINT              0x02010012
def zbTHERMOSTAT_CLUSTER_SYSTEM_MODE                            0x0201001C
def zbTHERMOSTAT_CLUSTER_THERMOSTAT_PROGRAMMING_OPERATION_MODE  0x02010025
def zbTHERMOSTAT_CLUSTER_CONTROL_SEQUENCE_OF_OPERATION          0x0201001B
def zbTHERMOSTAT_CLUSTER_MIN_HEAT_SETPOINT_LIMIT                0x02010015
def zbTHERMOSTAT_CLUSTER_MAX_HEAT_SETPOINT_LIMIT                0x02010016
def zbTHERMOSTAT_CLUSTER_MIN_COOL_SETPOINT_LIMIT                0x02010017
def zbTHERMOSTAT_CLUSTER_MAX_COOL_SETPOINT_LIMIT                0x02010018

scope 0 {

// We map Setpoint setpoint_type 0x01 (HEATING) and 0x02 (COOLING)
// The Z-Wave units are converted into milli units, in ZigBee is should be
// deci-celsius.
// A value scale of 1 means fahrenheit

// Heating
r'zbTHERMOSTAT_CLUSTER_OCCUPIED_HEATING_SETPOINT =
  if( r'zwTHERMOSTAT_SETPOINT_TYPE[1].zwTHERMOSTAT_SETPOINT_VALUE_SCALE == 0 )
    (r'zwTHERMOSTAT_SETPOINT_TYPE[1].zwTHERMOSTAT_SETPOINT_VALUE / 10)
  if( r'zwTHERMOSTAT_SETPOINT_TYPE[1].zwTHERMOSTAT_SETPOINT_VALUE_SCALE == 1 )
    (((r'zwTHERMOSTAT_SETPOINT_TYPE[1].zwTHERMOSTAT_SETPOINT_VALUE - 32000) * 5) / 90)
    undefined

d'zwTHERMOSTAT_SETPOINT_TYPE[1].zwTHERMOSTAT_SETPOINT_VALUE =
  if( r'zwTHERMOSTAT_SETPOINT_TYPE[1].zwTHERMOSTAT_SETPOINT_VALUE_SCALE == 0 )
    (d'zbTHERMOSTAT_CLUSTER_OCCUPIED_HEATING_SETPOINT*10)
  if( r'zwTHERMOSTAT_SETPOINT_TYPE[1].zwTHERMOSTAT_SETPOINT_VALUE_SCALE == 1 )
    (((d'zbTHERMOSTAT_CLUSTER_OCCUPIED_HEATING_SETPOINT)*90) / 5 + 32000)
    undefined

r'zbTHERMOSTAT_CLUSTER_MIN_HEAT_SETPOINT_LIMIT =
  if(r'zwTHERMOSTAT_SETPOINT_TYPE[1].zwTHERMOSTAT_SETPOINT_MIN_VALUE_SCALE == 0)
    (r'zwTHERMOSTAT_SETPOINT_TYPE[1].zwTHERMOSTAT_SETPOINT_MIN_VALUE / 10)
  if(r'zwTHERMOSTAT_SETPOINT_TYPE[1].zwTHERMOSTAT_SETPOINT_MIN_VALUE_SCALE == 1)
    (((r'zwTHERMOSTAT_SETPOINT_TYPE[1].zwTHERMOSTAT_SETPOINT_MIN_VALUE - 32000) * 5) / 90)
    undefined

r'zbTHERMOSTAT_CLUSTER_MAX_HEAT_SETPOINT_LIMIT =
  if(r'zwTHERMOSTAT_SETPOINT_TYPE[1].zwTHERMOSTAT_SETPOINT_MAX_VALUE_SCALE == 0)
    (r'zwTHERMOSTAT_SETPOINT_TYPE[1].zwTHERMOSTAT_SETPOINT_MAX_VALUE  / 10)
  if(r'zwTHERMOSTAT_SETPOINT_TYPE[1].zwTHERMOSTAT_SETPOINT_MAX_VALUE_SCALE == 1)
    (((r'zwTHERMOSTAT_SETPOINT_TYPE[1].zwTHERMOSTAT_SETPOINT_MAX_VALUE - 32000) * 5) / 90)
    undefined

// Cooling
r'zbTHERMOSTAT_CLUSTER_OCCUPIED_COOLING_SETPOINT =
  if( r'zwTHERMOSTAT_SETPOINT_TYPE[2].zwTHERMOSTAT_SETPOINT_VALUE_SCALE == 0)
    (r'zwTHERMOSTAT_SETPOINT_TYPE[2].zwTHERMOSTAT_SETPOINT_VALUE / 10 )
  if( r'zwTHERMOSTAT_SETPOINT_TYPE[2].zwTHERMOSTAT_SETPOINT_VALUE_SCALE == 1)
    (((r'zwTHERMOSTAT_SETPOINT_TYPE[2].zwTHERMOSTAT_SETPOINT_VALUE - 32000) * 5) / 90)
    undefined

r'zbTHERMOSTAT_CLUSTER_MIN_COOL_SETPOINT_LIMIT =
  if(r'zwTHERMOSTAT_SETPOINT_TYPE[2].zwTHERMOSTAT_SETPOINT_MIN_VALUE_SCALE == 0)
    (r'zwTHERMOSTAT_SETPOINT_TYPE[2].zwTHERMOSTAT_SETPOINT_MIN_VALUE  / 10)
  if(r'zwTHERMOSTAT_SETPOINT_TYPE[2].zwTHERMOSTAT_SETPOINT_MIN_VALUE_SCALE == 1)
    (((r'zwTHERMOSTAT_SETPOINT_TYPE[2].zwTHERMOSTAT_SETPOINT_MIN_VALUE - 32000) * 5) / 90)
    undefined

r'zbTHERMOSTAT_CLUSTER_MAX_COOL_SETPOINT_LIMIT =
  if(r'zwTHERMOSTAT_SETPOINT_TYPE[2].zwTHERMOSTAT_SETPOINT_MAX_VALUE_SCALE == 0)
    (r'zwTHERMOSTAT_SETPOINT_TYPE[2].zwTHERMOSTAT_SETPOINT_MAX_VALUE  / 10)
  if(r'zwTHERMOSTAT_SETPOINT_TYPE[2].zwTHERMOSTAT_SETPOINT_MAX_VALUE_SCALE == 1)
    (((r'zwTHERMOSTAT_SETPOINT_TYPE[2].zwTHERMOSTAT_SETPOINT_MAX_VALUE - 32000) * 5) / 90)
    undefined

d'zwTHERMOSTAT_SETPOINT_TYPE[2].zwTHERMOSTAT_SETPOINT_VALUE =
  if( r'zwTHERMOSTAT_SETPOINT_TYPE[2].zwTHERMOSTAT_SETPOINT_VALUE_SCALE == 0)
    (d'zbTHERMOSTAT_CLUSTER_OCCUPIED_COOLING_SETPOINT*10)
  if( r'zwTHERMOSTAT_SETPOINT_TYPE[2].zwTHERMOSTAT_SETPOINT_VALUE_SCALE == 1 )
    (((d'zbTHERMOSTAT_CLUSTER_OCCUPIED_COOLING_SETPOINT)*90) / 5 + 32000)
    undefined

// Local Temperature
r'zbTHERMOSTAT_CLUSTER_LOCAL_TEMPERATURE =
  if(r'zwSENSOR_MULTILEVEL_SENSOR_TYPE[1].zwSENSOR_MULTILEVEL_SCALE == 0)
    (r'zwSENSOR_MULTILEVEL_SENSOR_TYPE[1].zwSENSOR_MULTILEVEL_SENSOR_VALUE  / 10)
  if(r'zwSENSOR_MULTILEVEL_SENSOR_TYPE[1].zwSENSOR_MULTILEVEL_SCALE == 1)
    (((r'zwSENSOR_MULTILEVEL_SENSOR_TYPE[1].zwSENSOR_MULTILEVEL_SENSOR_VALUE - 32000) * 5) / 90)
    undefined


r'zbTHERMOSTAT_CLUSTER_SYSTEM_MODE =
  if (r'zwTHERMOSTAT_MODE == 0) 0
  if (r'zwTHERMOSTAT_MODE == 1) 4
  if (r'zwTHERMOSTAT_MODE == 2) 3
  if (r'zwTHERMOSTAT_MODE == 3) 1
  if (r'zwTHERMOSTAT_MODE == 6) 7
  if (r'zwTHERMOSTAT_MODE == 8) 8
  undefined

d'zwTHERMOSTAT_MODE =
  if ( d'zbTHERMOSTAT_CLUSTER_SYSTEM_MODE == 0) 0
  if ( d'zbTHERMOSTAT_CLUSTER_SYSTEM_MODE == 4) 1
  if ( d'zbTHERMOSTAT_CLUSTER_SYSTEM_MODE == 3) 2
  if ( d'zbTHERMOSTAT_CLUSTER_SYSTEM_MODE == 1) 3
  if ( d'zbTHERMOSTAT_CLUSTER_SYSTEM_MODE == 7) 6
  if ( d'zbTHERMOSTAT_CLUSTER_SYSTEM_MODE == 8) 8
  undefined


r'zbTHERMOSTAT_CLUSTER_CONTROL_SEQUENCE_OF_OPERATION =
  if(((r'zwTHERMOSTAT_SUPPORTED_MODES) & 6) == 0x06) 0x04
  if(((r'zwTHERMOSTAT_SUPPORTED_MODES) & 6) == 0x02) 0x02
  undefined

//just set this to 0
r'zbTHERMOSTAT_CLUSTER_THERMOSTAT_PROGRAMMING_OPERATION_MODE = if( r'zwTHERMOSTAT_MODE ) 0 undefined

}
